

Sample domain file (benchmarks/blocks/domain.pddl):

;;; 4 Op-blocks world
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (domain BLOCKS)
  (:requirements :strips :typing)
  (:types block)
  (:predicates (on ?x - block ?y - block)
	       (ontable ?x - block)
	       (clear ?x - block)
	       (handempty)
	       (holding ?x - block)
	       )

  (:action pick-up
	     :parameters (?x - block)
	     :precondition (and (clear ?x) (ontable ?x) (handempty))
	     :effect
	     (and (not (ontable ?x))
		   (not (clear ?x))
		   (not (handempty))
		   (holding ?x)))

  (:action put-down
	     :parameters (?x - block)
	     :precondition (holding ?x)
	     :effect
	     (and (not (holding ?x))
		   (clear ?x)
		   (handempty)
		   (ontable ?x)))
  (:action stack
	     :parameters (?x - block ?y - block)
	     :precondition (and (holding ?x) (clear ?y))
	     :effect
	     (and (not (holding ?x))
		   (not (clear ?y))
		   (clear ?x)
		   (handempty)
		   (on ?x ?y)))
  (:action unstack
	     :parameters (?x - block ?y - block)
	     :precondition (and (on ?x ?y) (clear ?x) (handempty))
	     :effect
	     (and (holding ?x)
		   (clear ?y)
		   (not (clear ?x))
		   (not (handempty))
		   (not (on ?x ?y)))))


Sample task file (benchmarks/blocks/task01.pddl):

(define (problem BLOCKS-4-0)
(:domain BLOCKS)
(:objects D B A C - block)
(:INIT (CLEAR C) (CLEAR A) (CLEAR B) (CLEAR D) (ONTABLE C) (ONTABLE A)
 (ONTABLE B) (ONTABLE D) (HANDEMPTY))
(:goal (AND (ON D C) (ON C B) (ON B A)))
)


Execute the Python interpreter on /src/pyperplan.py using the command line (this depends on the OS):

usage: pyperplan.py [-h] [-l {debug,info,warning,error}]
                    [-H {hff,hmax,lmcut,hadd,hsa,landmark,blind}]
                    [-s {gbf,ids,astar,wastar,bfs,sat,ehs}]
                    [domain] problem

./src/pyperplan.py -s astar benchmarks/blocks/domain.pddl benchmarks/blocks/task01.pddl


MacBook-Pro-di-Gianpaolo-Ghiani-2:Pyperplan GG$ ./src/pyperplan.py -s astar benchmarks/blocks/domain.pddl benchmarks/blocks/task01.pddl
2019-10-31 10:05:58,320 INFO     using search: astar_search
2019-10-31 10:05:58,320 INFO     using heuristic: hFFHeuristic
2019-10-31 10:05:58,320 INFO     Parsing Domain /Users/GG/Documents/Didattica/Corsi 2019:20/Decision Support Systems - 1st term/Python codes/Pyperplan/benchmarks/blocks/domain.pddl
2019-10-31 10:05:58,322 INFO     Parsing Problem /Users/GG/Documents/Didattica/Corsi 2019:20/Decision Support Systems - 1st term/Python codes/Pyperplan/benchmarks/blocks/task01.pddl
2019-10-31 10:05:58,322 INFO     5 Predicates parsed
2019-10-31 10:05:58,322 INFO     4 Actions parsed
2019-10-31 10:05:58,322 INFO     4 Objects parsed
2019-10-31 10:05:58,323 INFO     0 Constants parsed
2019-10-31 10:05:58,323 INFO     Grounding start: blocks-4-0
2019-10-31 10:05:58,324 INFO     Relevance analysis removed 0 facts
2019-10-31 10:05:58,324 INFO     Grounding end: blocks-4-0
2019-10-31 10:05:58,324 INFO     29 Variables created
2019-10-31 10:05:58,324 INFO     40 Operators created
2019-10-31 10:05:58,324 INFO     Search start: blocks-4-0
2019-10-31 10:05:58,325 INFO     Initial h value: 6.000000
2019-10-31 10:05:58,331 INFO     Goal reached. Start extraction of solution.
2019-10-31 10:05:58,331 INFO     11 Nodes expanded
2019-10-31 10:05:58,331 INFO     Search end: blocks-4-0
2019-10-31 10:05:58,331 INFO     Wall-clock search time: 0.007
2019-10-31 10:05:58,331 INFO     Plan length: 6
2019-10-31 10:05:58,334 INFO     validate could not be found on the PATH so the plan can not be validated.
MacBook-Pro-di-Gianpaolo-Ghiani-2:Pyperplan GG$ 

The plan is in a .soln file like task01.pddl.soln
(pick-up b)
(stack b a)
(pick-up c)
(stack c b)
(pick-up d)
(stack d c)



